rule get_flanking_coordinates:
	 #prepare flanking coordinates centering on binding site
	 input:
		  "{sample}.bed"
	 output:
		  "{sample}-summit-100bp.bed"
	 shell:
		  "awk 'BEGIN{{ OFS=\"\t\";}}"
		  "{{ midPos=int(($2+$3)/2); start= midPos-50; end = midPos+50;"
		  "if (start <0) {{start = 1 ; end=100}};"
		  "print $1, start, end, $4, $5, $6, $2, $3;}}'  {input} > {output}"

rule get_fasta:
	 input:
		  "{sample}-summit-100bp.bed",
		  genome_reference
	 output:
		  "{sample}-summit-100bp.fa"
	 shell:
		  "fastaFromBed -s -fi {input[1]} -bed  {input[0]} -fo {output}"

rule run_meme_chip:
	 input:
		  "{sample}-summit-100bp.fa"
	 output:
		  "{sample}_memechip_output"
	 shell:
		  "meme-chip -meme-maxw 8 -norc -oc {output}"
		  " -db {RCAS_path}/src/base/Homo_sapiens-U2T.meme"
		  " -db {RCAS_path}/src/base/Mus_musculus-U2T.meme"
		  " -db {RCAS_path}/src/base/Drosophila_melanogaster-U2T.meme"
		  " -db {RCAS_path}/src/base/Caenorhabditis_elegans-U2T.meme {input}"

rule profile_top_motifs:
	 input:
		  "{sample}_memechip_output",
		  "{sample}-summit-100bp.bed",
		  "{sample}.anot.tsv"
	 output:
		  "{sample}.anot-motif.tsv"
	 shell:
		  "python2 {RCAS_path}/src/top_motifs.py"
		  " -m {input[0]}/centrimo_out/centrimo.html"
		  " -e {input[0]}/centrimo_out/centrimo.txt"
		  " -c {input[1]} -a {input[2]} > {output}"
