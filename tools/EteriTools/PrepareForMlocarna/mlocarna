#!/usr/bin/perl -I/usr/local/user/locarna/1.7.16/lib/perl
# -*- perl -*-

### ============================================================================
### MAN-PAGE
###

=head1 NAME

MLocarna - multiple alignment of RNA (LocARNA 1.7.16)

=head1 SYNOPSIS

mlocarna [options] <fasta file>

=head1 DESCRIPTION

B<MLocarna> computes a multiple sequence-structure alignment of RNA
sequences.  

=head1 OPTIONS

=head2 Controlling Output

=over 4

=item  B<--tgtdir>

Target directory. All output files are written to this directory.  Per
default the target directory is generated from the input filename by
replacing suffix fa by (or appending) out.

=item  B<-v, --verbose>

Turn on verbose ouput.

=item  B<--moreverbose>

Be even more verbose

=item  B<-q, --quiet>

Be quiet.

=item B<--keep-sequence-order>

Preserve sequence order of the input in the final alignment.  Affects
output to stdout and results/result.aln.

=back 

=head2 Controlling pairwise alignments

=over 4

=item  B<--noLP> / B<--LP>                       

Disallow/Allow lonely pairs (default: Disallow).

=item  B<--free-endgaps>

Allow free endgaps. (Corresponds to pairwise locarna option --free-endgaps "++++".)

=item  B<--free-endgaps-3>

Allow free endgaps 3'.

=item  B<--free-endgaps-5>

Allow free endgaps 5'.

=item B<Other locarna options>

Many of the options of locarna, the program for pairwise alignment, will work as well.
This allows controlling scoring, locality, and speed/acccuracy trade off (heuristics).
B<Please see> C<locarna -h> or the manpage of locarna.

=item B<--pw-aligner>

Utilize given binary for the pairwise alignment (overrides def=locarna).

=item B<--pw-aligner-p>

Utilize given binary for the probabilistic pairwise alignment
(overrides def=locarna_p).

=item B<--pw-aligner-options>

Additional option string for the pairwise alignment (def="").

=item B<--pw-aligner-p-options>

Additional option string for the probabilistic pairwise alignment (def="").

=back

=head2 Controlling guide tree construction 

=over 4

=item  B<--treefile>                    

File with guide tree in NEWICK format. The given tree is used as guide
tree for the progressive alignment. This saves the calculation of
pairwise all-vs-all similarities and construction of the guide tree.

=item B<--similarity-matrix> 

File with similarity matrix. The similarities in the matrix are used
to construct the guide tree for the progressive alignment. This saves
the calculation of pairwise all-vs-all similarities.


=item B<--graphkernel>

Use the graphkernel for constructing the guide tree. 

=item B<--svmsgdnspdk[=<program>]>

Specify the svmsgdnspdk program (potentially including path). Default:
use "svmsgdnspdk" in path.

=item B<--fasta2shrep=<program>>

Program "fasta2shrep" for generating graphs from the input sequences
for use with the graph kernel guide tree generation (potentially
including path). Default: use "fasta2shrep_gspan.pl" in path.

=item B<--fasta2shrep-options=<args>>

Command line arguments for fasta2shrep. Default: "-wins 200 -shift 50
-stack -t 3 -M 3".

=item B<-P, --tree-min-prob=<f>>

Minimal prob for constructing guide tree. This probability can be set
separately for the all-2-all comparison for constructing the guide
tree and the progressive/iterative alignment steps.

=back


=head2 Controlling multiple alignment

=over 4

=item B<--alifold-consensus-dp>

Employs B<RNAalifold -p> for generating consensus dotplot after each
progressive alignment step. This replaces the default consensus
dotplot computation, which averages over the input dot plots.  This
method should be used with care in combination with structural
constraints, since it ignores them for all but the pairwise alignments
of single sequences. Furthermore, note that it does not support
B<--stacking>.

=item B<--local-progressive>

Align only the subalignment of locally aligned subsequences in
subsequent steps of the progressive multiple alignment. Note: this is
only effective if local alignment is turned on. (Default for
sequence local alignment; turn off by B<--global-progressive>)

=item B<--global-progressive> 

Use alignments including "locality gaps" in subsequent steps of the
progressive multiple alignment. Note: this is only effective if local
alignment is turned on. (Opposite of B<--local-progressive>)

=item B<--max-diff-aln=<file>> 

Restrict maximal difference to the alignment in <file> in clustalw
format (difference given by --max-diff). Use this option for
constrained re-aligning.

=item B<--probabilistic> 

Score alignments using match probabilities that are computed by a
partition function approach. This makes possible to
consistency-transform the probabilities (option
--consistency-transform) and to compute reliabilities.  Reliabilities
can also be used for iterating the alignment with reliably aligned
base pairs as structural constraints (option --it-reliable-structure).

=item B<--consistency-transformation> 

Apply probabilistic consistency transformation (only possible in
probabilistic mode).

=item B<--iterate> 

Refine iteratively after progressive alignment. Currently, iterative
refinement optimizes the SCI or RELIABILITY (not the locarna score)! Iterative
refinement realigns all binary splits along the guide tree.

=item  B<--iterations=<num>>                  

Refine iteratively for given number of iterations (or stop at
convergence).

=item  B<--extlib>

Use library extension for base pair probabilities (experimental/not functional).

=item  B<--it-reliable-structure=<num>> 

Iterate alignment <num> times with reliable structure. This works only
in probabilistic mode, when reliabilities can be computed.

=back

=head2 Options for probabilistic mode

=over 4

=item  B<--pf-only-basematch>           

Use only base match probabilities (no base pair match probabilities).


=item  B<--pf-scale=<scale>>

Scale of partition function; use for avoiding overflow in larger instances.


=item  B<--fast-mea>

Compute base match probabilities using Gotoh PF-algorithm.


=item  B<--mea-alpha>

Weight of unpaired probabilities in fast mea mode.


=item  B<--mea-beta>

Weight of base pair match contribution in probabilistic mode.


=item  B<--mea-gamma>

Reserved parameter for fast-mea mode.

=item  B<--mea-gapcost>

Turn on gap penalties in probabilistic/mea mode (default: off).

=item  B<--no-write-bm-probs>

Don't write base match probabilities to files in target dir.

=item  B<--no-write-am-probs>           

Don't write arc match probabilities to files in target dir.

=back

=head2 Special modes of operation

=over 4

=item  B<--dp-cache=<dir>>

Use directory <dir> as cache for dp files.

=item  B<--only-dps>

Compute only the missing dp files, don't align.

=item  B<--evaluate=<file>>
  
Evaluate the given multiple alignment (clustalw aln format, or use --eval-fasta)

=item  B<--eval-fasta>
  
Assume that alignment for evaluation is in fasta format

=back

=head2 Constraints

=over 4

=item  B<--ignore-constraints>          

Ignore constraints even if given.

=back

=head2 Rna folding (RNAfold/RNAplfold)

=over 4

=item  B<--plfold-span=span>

Use RNAplfold with span

=item  B<--plfold-winsize=ws>
 
Use RNAplfold with window of size ws (default=2*span)

=item  B<--rnafold-parameter=<file>>

Parameter file for RNAfold (RNAfold's -P option)

=item  B<--skip-pp>                     

Skip computation of pair probs if the probabilities are already
existing. Non-existing ones are still computed.

=item B<--no-bpp-precomputation>

Switch off precomputation of base pair probabilties. Overwrite
potentially existing input files.  (compare skip-pp). For use with
special pairwise aligners (e.g. locarna_n) that recompute the base
pair probabilities at each invokation.

=item B<--in-loop-probabilities>

Turn on precomputation of in loop probabilties. For use with special
pairwise aligners (e.g. locarna_n) that use such probabilities.

=back

=head2 Multithreading

=over 4

=item  B<--threads=<num>> or B<--cpus=<num>>

Use <num> threads in parallel (support multicore/processor).

=back

=head2 Getting Help

=over 4

=item  B<--help>

Brief help message

=item  B<--man>

Full documentation

=back

The sequences are given in input file <file> in mfasta
format.  All results are written to a target directory <dir>. If the
file tree is given, contained tree (in NEWICK-tree format) is used as
guide tree for the progressive alignment. The final results are
collected in <tgtdir>/results. The final multiple alignment is
<tgtdir>/results/result.aln.

Whenever parameters are not specified explicitly, we use the locarna defaults 
(please see C<locarna -h> or the manpage of locarna).


=head1 AUTHOR

Sebastian Will

=cut

use MLocarna;
use MLocarna::SparseMatrix;
use MLocarna::Trees;
use MLocarna::MatchProbs;
use MLocarna::Aux;

use strict;
use warnings;

use Cwd 'abs_path';

use File::Path;

########################################
## threading support
##
use Config;

my $THREADS_ENABLED = $Config{useithreads};

if ($THREADS_ENABLED) {
    require MLocarna::threaded;
    import MLocarna::threaded;
} else {
    require MLocarna::unthreaded;
    import MLocarna::unthreaded;
}

## ------------------------------------------------------------
## global constants


$MLocarna::PACKAGE_STRING="LocARNA 1.7.16";

my $prefix = "/usr/local/user/locarna/1.7.16";
my $exec_prefix = "${prefix}";
my $bindir = "${exec_prefix}/bin";

## vienna package programs
my $RNAfold = "/usr/local/user/ViennaRNA/2.1.7/bin/RNAfold";
my $RNAplfold = "/usr/local/user/ViennaRNA/2.1.7/bin/RNAplfold";
$MLocarna::RNAalifold = "/usr/local/user/ViennaRNA/2.1.7/bin/RNAalifold";

## test whether rnafold_pp is working
my $HAVE_RNAFOLD_PP = readpipe("$bindir/locarna_rnafold_pp --TEST");

my $input_dir="input";
my $probs_dir="probs";
my $intermediate_dir="intermediates";
my $results_dir="results";

## turn off computation of reliability profiles/dotplots for single
## sequences
my $reliabilities_single_sequences = 1;
## control where to write single arcmatch rels
my $single_reliabilities_dir="single_reliabilities"; 

##------------------------------------------------------------
## options

# we use the GetOpt module in a mode that allows
# to pass a list of options quite easily to locarna
# while handling other options explicitely

use Getopt::Long;
use Pod::Usage;

my $help;
my $man;

my $version;

my $quiet;
my $verbose;

my $keep_sequence_order;

my $noLP=1;
my $no_lonely_pair;
my $lonely_pair;

my $free_endgaps;
my $free_endgaps_3;
my $free_endgaps_5;

my $pw_aligner="$bindir/locarna";
my $pw_aligner_p="$bindir/locarna_p";

my $pw_aligner_options="";
my $pw_aligner_p_options="";

my $tree_file;
my $similarity_matrix_file;
my $tgtdir;

my $graphkernel;
my $svmsgdnspdk="svmsgdnspdk";
my $svmsgdnspdk_radius=2;
my $svmsgdnspdk_distance=4;

my $fasta2shrep="fasta2shrep_gspan.pl";


##########
# default options of fasta2shrep. In particular, specify
# -wins window size
# -shift window shift in percent of window size
# -stack extra stack vertices 
# -t grammar abstraction level (of RNAshape) 
# -M number of shapes
#
my $fasta2shrep_options="-wins 200 -shift 50 -stack -t 3 -M 3";

my $seqs_file; ## filename of fasta file with input sequences 

my $skip_pp=0; ## skip the computation of pair probabilities for files
               ## that exist already 

my $ext_library;

my $iterate;
my $iterations;

my $it_reliable_structure=0;

my $min_prob=0.0005;
my $tree_min_prob; #=0.01;

my $mea_alpha;
my $mea_beta;
my $mea_gamma;
my $mea_gapcost;

my $stacking;

my $dp_cache; ## use this directory as a cache for dotplot-files (used to avoid multiple computation)
my $only_dps; # compute only the missing dp files (used to fill the cache!)

my $moreverbose;

my $exp_prob;

my $ignore_constraints;


my $consistency_transformation;

my $opt_no_write_bm_probs;  ## get info about alignment edge probabilities
my $opt_no_write_am_probs;  ## get info about alignment structural edge probabilities

my $opt_write_bm_probs;  ## get info about alignment edge probabilities
my $opt_write_am_probs;  ## get info about alignment structural edge probabilities


my $probabilistic;

my $pf_scale;

my $only_basematch_probs; # use only basematch probabilities, no base pair match probabilities

my $fast_mea;

my $rnafold_parameter_file;

my $plfold_span;
my $plfold_winsize;

my $thread_number=1;

my $write_structure;

my $evaluate_alignment;

my $opt_eval_fasta;

my $opt_no_bpp_precomputation;

my $opt_in_loop_probabilities;

my $width;

my $max_diff_aln;

my $alifold_consensus_dp;

my $sequ_local;

my $opt_local_progressive;
my $opt_global_progressive;

Getopt::Long::Configure("no_ignore_case");

my %options_hash=
    (
     "tree-min-prob" => \$tree_min_prob,
     "noLP" => \$no_lonely_pair,
     "LP" => \$lonely_pair,
     
     "free-endgaps" => \$free_endgaps,
     "free-endgaps-3" => \$free_endgaps_3,
     "free-endgaps-5" => \$free_endgaps_5,

     "pw-aligner" => \$pw_aligner,
     "pw-aligner-p" => \$pw_aligner_p,
     
     "pw-aligner-options" => \$pw_aligner_options,
     "pw-aligner-p-options" => \$pw_aligner_options,
     
     "min-prob" => \$min_prob,
     "tree-min-prob" => \$tree_min_prob,

     "stacking" => \$stacking,

     "extlib" => \$ext_library,
     
     "skip-pp" => \$skip_pp,

     ## consensus dot plot computation
     ## This functionality is moved to locarna binary.
     ## (see autopass options)
     # "alifold-consensus-dp" => \$alifold_consensus_dp,
     
     ## realigning
     
     "max-diff-aln" => \$max_diff_aln,

     ## iterative alignment
     "iterate" => \$iterate,
     "iterations" => \$iterations,

     "it-reliable-structure" => \$it_reliable_structure,

     "ignore-constraints" => \$ignore_constraints,
     
     "treefile" => \$tree_file,

     "similarity-matrix" => \$similarity_matrix_file,

     "graphkernel" => \$graphkernel,
     "svmsgdnspdk" => \$svmsgdnspdk,
     "svmsgdnspdk-radius" => \$svmsgdnspdk_radius,
     "svmsgdnspdk-distance" => \$svmsgdnspdk_distance,
     "fasta2shrep" => \$fasta2shrep,
     "fasta2shrep-options" => \$fasta2shrep_options,
     
     "outdir" => \$tgtdir,

     # controlling the mode of operation
     
     "probabilistic" => \$probabilistic, ## turn on mea alignment, default: pf for arc and base match
     
     "pf-scale" => \$pf_scale,
     
     "only-basematch-probs" => \$only_basematch_probs, ## pf only for basematch
     "fast-mea" => \$fast_mea,                         ## use heuristics for base match probabilities
     
     "min-bm-prob" => \$MLocarna::MatchProbs::min_bm_prob, ## locarna_p option
     "min-am-prob" => \$MLocarna::MatchProbs::min_am_prob, ## locarna_p option
     
     
     ## whether to apply (probabilistic) consistency transformation
     "consistency-transformation" => \$consistency_transformation,
     

     ## mea weights
     "mea-alpha" => \$mea_alpha,
     "mea-beta" => \$mea_beta,
     "mea-gamma" => \$mea_gamma,

     "mea-gapcost" => \$mea_gapcost,

     ## caching pair probabilities
     "dp-cache" => \$dp_cache,
     "only-dps" => \$only_dps,
     
     
     "no-write-bm-probs" => \$opt_no_write_bm_probs,
     "no-write-am-probs" => \$opt_no_write_am_probs,

     "write-bm-probs" => \$opt_write_bm_probs,
     "write-am-probs" => \$opt_write_am_probs,


     ## energy parameter for RNA fold
     "rnafold-parameter" => \$rnafold_parameter_file,


     ## plfold
     "plfold-span" => \$plfold_span,
     "plfold-winsize" => \$plfold_winsize,

     
     ## parallel processing
     "cpus" => \$thread_number,

     ## parallel processing
     "threads" => \$thread_number,
     
     ## evaluation of a given alignment
     
     "evaluate" => \$evaluate_alignment,
     
     "eval-fasta" => \$opt_eval_fasta,

     # base pair probability precomputation
     "no-bpp-precomputation" => \$opt_no_bpp_precomputation,

     # in-loop probability precomputation
     "in-loop-probabilities" => \$opt_in_loop_probabilities,
     
     ## output
     "write-structure" => \$write_structure,

     "width" => \$width,

     ## "standard" options
     
     "verbose" => \$verbose,
     "moreverbose" => \$moreverbose,
     
     "quiet" => \$quiet,
     
     "keep-sequence-order" => \$keep_sequence_order,

     "local-progressive" => \$opt_local_progressive,
     
     "global-progressive" => \$opt_global_progressive,
     
     "version" => \$version,

     "help"=> \$help,
     "man" => \$man
     
    );


my @options = #options that are not or not automatically passed to locarna
    (
     "noLP",
     "LP",
     
     "free-endgaps",
     "free-endgaps-3",
     "free-endgaps-5",

     "pw-aligner=s",
     "pw-aligner-p=s",

     "pw-aligner-options=s",
     "pw-aligner-p-options=s",
     
     "min-prob|p=f",
     "tree-min-prob|P=f",
     
     "stacking",
     
     "extlib",
     
     "skip-pp",

#     "alifold-consensus-dp",
     
     "max-diff-aln=s",

     "iterate",
     "iterations=i",
     
     "it-reliable-structure=i",

     "ignore-constraints",
     "consistency-transformation",

     "treefile=s",
     "similarity-matrix=s",

     "graphkernel",
     "svmsgdnspdk=s",
     "svmsgdnspdk-radius=s",
     "svmsgdnspdk-distance=s",
     "fasta2shrep=s",
     "fasta2shrep-options=s",

     "outdir|tgtdir=s",

     "min-bm-prob=f",
     "min-am-prob=f",

     "no-write-bm-probs",
     "no-write-am-probs",
     "write-bm-probs",
     "write-am-probs",

     "mea-alignment",
     "mea-alpha=i",
     "mea-beta=i",
     "mea-gamma=i",
     "mea-gapcost",

     "probabilistic",
     "pf-scale=f",
     "only-basematch-probs",
     "fast-mea",
     
     "dp-cache=s",
     "only-dps",

     "rnafold-parameter=s",

     ## plfold
     "plfold-span=i",
     "plfold-winsize=i",

     "cpus=i",
     "threads=i",

     "evaluate=s",
     "eval-fasta",
     
     "no-bpp-precomputation",
     
     "in-loop-probabilities",

     "write-structure",
     
     "width=i",

     "keep-sequence-order",

     "local-progressive",
     "global-progressive",
     
     "verbose|v",
     "moreverbose",
     "quiet",
     
     "version",

     "help",
     "man"
    );


# options with argument that are passed to locarna
# autopass options will always be passed to locarna for tree computation 
# and progressive steps *and* to locarna_p
#
my @autopass_arg_options = 
    (
     "match|m=i",
     "mismatch|M=i",
     "indel|i=i",
     "indel-opening=i",
     "ribosum-file=s",
     "use-ribosum=s",
     "struct-weight|s=i",
     "tau|t=i",
     "exclusion|E=i",
     "exp-prob|e=f",

     "max-diff-am=i",
     "max-diff=i",
     
     "struct-local=s",
     "sequ-local=s",
     
     "probcons-file=s",
     "match-prob-method=i",
    
     "temperature=i",
     "pf-struct-weight=i"
     
    );

my @autopass_noarg_options = ( "max-diff-relax", "alifold-consensus-dp" );

# -----------------------------------
# some default values
$options_hash{"struct-weight"}=200;
$options_hash{"max-diff-am"}=30;


GetOptions( \%options_hash, @options, @autopass_arg_options, @autopass_noarg_options )
    || pod2usage(2);


help_msg() if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;


## option --version

if ($version) {
   system("$bindir/locarna --version");
   exit(0);
}


# parameter mfasta file
if ($#ARGV == 0) {
    $seqs_file = $ARGV[0];
}else {
    help_msg();
}



sub help_msg {
    print STDERR 
"Usage:
    mlocarna [options] <fasta file>
    Multiply aligns the RNA sequences in the fasta file based on sequence-structure similarity.
    For details on options call mlocarna --man.
";
exit 0;
}


## ----------------------------------------
## verbosity level
if ($quiet) {
    $MLocarna::Aux::verbosemode=4;
}

# mode 3 is 'normal' verbosity

if ($verbose) {
    $MLocarna::Aux::verbosemode=2;
}

if ($moreverbose) {
    $verbose=1;
    $MLocarna::Aux::verbosemode=1;
}



## ------------------------------------------------------------
## write header

printmsg 3,"mLocARNA --- multiple Local (and global) Alignment of RNA --- ";
printmsg 3,readpipe("$bindir/locarna --version");
printmsg 3,"Copyright Sebastian Will\n\n";


## target/output directory
if (!defined($tgtdir)) {
    $tgtdir = $seqs_file;
    $tgtdir =~ s/\.mfasta$|\.fasta$|\.fa$//;
    $tgtdir .= ".out";
}

if (defined($evaluate_alignment)) {
    printmsg 3, "EVALUATION MODE\n\n";
    
    run_evaluation_mode($evaluate_alignment,"$tgtdir/probs");
    
    exit 0;
}

if (defined($lonely_pair) && defined($no_lonely_pair)) {
    printerr "Only one of the options --noLP and --LP can be defined at a time.\n";
    pod2usage(1);
}
if (defined($lonely_pair)) {
    $noLP=0;
}
if (defined($no_lonely_pair)) {
    $noLP=1;
}



if (exists $options_hash{"exp-prob"}) {
    $exp_prob=$options_hash{"exp-prob"};
}

## construct parameter string for locarna

my $locarna_params="$pw_aligner_options";
my $locarna_params_tree="$pw_aligner_options";

my $locarna_p_params="$pw_aligner_p_options";

## first set parameters that are equal for tree construction and other locarna runs
## (we copy locarna_params to locarna_params_tree later)

my $endgaps_string="++++";
if (defined($free_endgaps_3)) {
    $free_endgaps=1;
    $endgaps_string="-+-+" unless defined($free_endgaps_5);
} elsif (defined($free_endgaps_5)) {  
    $free_endgaps=1;
    $endgaps_string="+-+-";
}
if (defined($free_endgaps)) {
    if($probabilistic) {
	printerr "ERROR: no free-end gaps in probabilistic mode.\n";
	exit(-1);
    } else {
	$locarna_params .= "--free-endgaps=\"$endgaps_string\"";
    }
}

if (defined($only_basematch_probs)) { $locarna_p_params.=" --include-am-in-bm"; }


if (defined($MLocarna::MatchProbs::min_bm_prob)) { $locarna_p_params.=" --min-bm-prob=$MLocarna::MatchProbs::min_bm_prob"; }
if (defined($MLocarna::MatchProbs::min_am_prob)) { $locarna_p_params.=" --min-am-prob=$MLocarna::MatchProbs::min_am_prob"; }

if (defined($pf_scale)) { $locarna_p_params.=" --pf-scale $pf_scale"; }

## handle plfold
if (defined($plfold_span)) {
    printmsg 1,"Use plfold for local folding.\n";
    if (!defined($plfold_winsize)) {
	$plfold_winsize = 2*$plfold_span;
    }
    $ignore_constraints=1;
    #printerr "Warning: can't use constraints with plfold\n";
    if (defined($stacking)) {
	printerr "Can't use stacking with plfold.\n";
	exit -1;
    }
} elsif (defined($plfold_winsize)) {
    printerr "Warning: winsize for plfold given, but no span. Winsize ignored.\n";
}

$locarna_params.=" -v" if ($verbose);

# ----------------------------------------
# handle autopass options
#
foreach my $opt (@autopass_arg_options) {
    $opt =~ s/[|=].*//;
    
    if (exists $options_hash{$opt}) {
	$locarna_params .= " --$opt $options_hash{$opt}";
    }
}

foreach my $opt (@autopass_noarg_options) {
    $opt =~ s/[|].*//;
    
    if (exists $options_hash{$opt}) {
	$locarna_params .= " --$opt";
    }
}
#
# ----------------------------------------


if (defined($width)) {
    $locarna_params .= " --width $width";
} else {
    $width=120;
}


$locarna_p_params = $locarna_p_params. " " . $locarna_params;

## options only passed to locarna (not to locarna_p)
$locarna_params.=" --noLP" if ($noLP);

if ($probabilistic) {
    $locarna_params .= " --mea-alignment";

    $locarna_params .= " --mea-gapcost" if $mea_gapcost;
    
    $locarna_params.=" --mea-beta $mea_beta" if defined($mea_beta);
	
    if ( defined($fast_mea) ) {
	$locarna_params.=" --mea-alpha $mea_alpha" if defined($mea_alpha);
	$locarna_params.=" --mea-gamma $mea_gamma" if defined($mea_gamma);
    }
}

## copy to tree params
$locarna_params_tree=$locarna_params;

## special treatment of min-prob and tree-min-prob

$locarna_p_params .= " --min-prob $min_prob" if (defined($min_prob));

if ($probabilistic && $only_basematch_probs) {
    # when using only basematch probabilities,
    # turn off structural scoring for locarna
    
    $locarna_params .= " --min-prob 1";
    $locarna_params_tree .= " --min-prob 1";
} else {

    $locarna_params .= " --min-prob $min_prob" if (defined($min_prob));
    
    if (defined($tree_min_prob)) {
	$locarna_params_tree.=" --min-prob $tree_min_prob";
    } else {
	$locarna_params_tree.=" --min-prob $min_prob" if (defined($min_prob));
    }
}

if ($write_structure) {
    $locarna_params .= " --write-structure";
}

##################################################
## use global or local alignments in subsequent progressive steps
##
## make local progressive default, if sequence local alignment is turned on
if ((grep $options_hash{"sequ-local"}, ("on","true","t","1"))
    && !$opt_global_progressive) {
    $opt_local_progressive=1;
}

if ($opt_local_progressive) {
    if ($opt_global_progressive) {
	printerr "Warning: local overrides global progressive alignment!\n"
    }
    $locarna_params .= " --local-file-output";
}


## no stacking for locarna_p
$locarna_params .= " --stacking" if ($stacking);
$locarna_params_tree .= " --stacking" if ($stacking);


######## ------------------------------------------------------------
#### do some consistency checks on the parameters
##
#
#

if ($consistency_transformation && !$probabilistic) {
    printerr "Consistency transformation is supported only in probabilistic mode. Exit.\n";
    exit -1;
}

if (!$probabilistic && ($opt_write_bm_probs || $opt_write_am_probs)) {
    printerr "Writing of probabilities only possible in probabilistic mode. Exit.\n";
    exit -1;
}

if (($fast_mea || $only_basematch_probs) && $opt_write_am_probs) {
    printerr "Writing of arc match probabilities requested, but their computation is disabled. Exit.\n";
    exit -1;
}


##### ------------------------------------------------------------
## realignment: max_diff_aln

my $refaln;
my $refaln_filename;

if (defined($max_diff_aln)) {
    
    $refaln_filename=abs_path($max_diff_aln);
    
    ## read alignment from file
    $refaln = read_clustalw_alnloh("$refaln_filename");
    
}

##### ------------------------------------------------------------


##### ----------------------------------------
#### Writing of probabilities

## write probabilities to disk by default
if ($probabilistic) {
    $opt_write_bm_probs = 1;
    $opt_write_am_probs = 1;
}

## turn off writing of probabilities to disk if wanted
if (defined($opt_no_write_bm_probs)) { $opt_write_bm_probs = !$opt_no_write_bm_probs; }
if (defined($opt_no_write_am_probs)) { $opt_write_am_probs = !$opt_no_write_am_probs; }


####
## get full, absolute path for parameter file
if (defined($rnafold_parameter_file)) {
    $rnafold_parameter_file = abs_path($rnafold_parameter_file);
}



#### ------------------------------------------------------------

if ($probabilistic && ! $fast_mea) {
    printmsg 2,"Locarna-P parameters (for computing probabilities): $locarna_p_params\n";
}
printmsg 2,"Locarna parameter for progressive alignment: $locarna_params\n";
printmsg 2,"Locarna parameter for guide tree construction: $locarna_params_tree\n";

##------------------------------------------------------------

my $global_tmpname="_tmp_"; # valid file name, at max 12 characters
my $global_tmpprefix="$global_tmpname"; # could be a absolute path, but there's no need to


###------------------------------------------------------------
## Forward references for subs
#
#
sub compute_all_dotplots( $ );
sub compute_match_probs( $$$$$$$ );

### ============================================================================
### ============================================================================
### ============================================================================
## ---------------------------------------------------------------------------
# main program
#

## GLOBAL VARS (since passing this arround is ugly:))
my %bmprobs; # base match probabilities
my %amprobs; # arc match probabilities

## use for storing untransformed probs 
## in case of consistency-transformation
my %bmprobs_nocbt; # untransformed base match probabilities
my %amprobs_nocbt; # untransformed arc match probabilities


## CALL main
main();

exit(0);

sub main {
        
    if (! -e $tgtdir) {
	mkdir $tgtdir;
	if (! -e $tgtdir ) {
	    printerr "Cannot create target directory $tgtdir. Exit.";
	    exit -1;
	}
    } else {
	if ( -d $tgtdir ) {
	    printerr "Warning: $tgtdir exists already.\n" if ($verbose);
	} else {
	    printerr "$tgtdir is not a directory. Exit.\n";
	    exit -1;
	} 
    }
    
    ## ----------------------------------------
    # read input sequences from mfasta file
    my $seqs = read_fasta($seqs_file); ## input sequences
    
    register_normalized_seqnames($seqs); ## register normalized names for them that can be used as filenames

    ## print_normalized_sequence_names_hash();

    ## check input: we need at least two input sequences
    if (@$seqs == 0) {
	printerr("No input sequences found in $seqs_file.\n");
	exit(-1);
    } 
    if (@$seqs == 1) {
	printerr("Only one input sequence found in $seqs_file.\n");
	exit(-1);
    }

    
    check_constraints_in_fasta($seqs);
    
    ## check whether sequences and reference alignment are compatible
    if (defined($refaln) && (! is_aln_of_seqs($seqs,$refaln)) ) {
	print STDERR "Inconsistent input: Given reference alignment ( --max-diff-aln \"$max_diff_aln\" ) does not match input sequences ( \"$seqs_file\" ).\n";
	exit(-1);
    }
    
    ## ----------------------------------------
    ## make target directory the current directory
    ## and make paths to later used files absolute
    ##
    if (defined($tree_file)) { $tree_file = abs_path($tree_file); }
    if (defined($similarity_matrix_file)) { 
	$similarity_matrix_file = abs_path($similarity_matrix_file); }
    if (defined($dp_cache))  { $dp_cache  = abs_path($dp_cache);  }
    
    mkdir "$tgtdir/$input_dir";
    if ($probabilistic) {mkdir "$tgtdir/$probs_dir";}
    mkdir "$tgtdir/$intermediate_dir";
    mkdir "$tgtdir/$results_dir";

    system "/bin/cp", "-f", $seqs_file, "$tgtdir/$input_dir/input.fa";
    perform_multiple_alignment($seqs);

    ## ----------------------------------------
    # compute reliabilities and print reliability plots
    #
    if ($probabilistic) {
	
        ## iterate $it_reliable_structure times using reliable structures as structure constraints 
	for (my $it=0; $it<$it_reliable_structure; $it++) {
	    my $aln = read_aln_wo_anno("$tgtdir/$results_dir/result.aln");
	    
	    my $reliable_structures = compute_and_write_reliabilities($aln);
	    
	    printmsg 3, "\nIterate alignment with structure constraints from reliability information.\n";
	    
	    ## add constraints for new round of iteration
	    constrain_sequences($seqs,$reliable_structures);
	    
	    perform_multiple_alignment($seqs);
	}
	
	my $aln = read_aln_wo_anno("$tgtdir/$results_dir/result.aln");
	compute_and_write_reliabilities($aln);

    } ## end if probabilistic
    


} ## end main


### ------------------------------------------------------------
### ------------------------------------------------------------
### parse pseudoknotted dot-bracket structures
### and generate pp file for fixed structure
###

sub parse_bracket_structure_single {
    my ($str,$open,$close,$str_array_ref)=@_;
    
    my @str_array = @{ $str_array_ref };
    
    my @stack;
    
    for (my $i=0; $i<length($str); $i++) {
	my $c=substr $str,$i,1;
	
	if ($c eq $open) {
	    push @stack,$i;
	} elsif ($c eq $close) {
	    my $j=pop @stack;
	    $str_array[$i]=$j;
	    $str_array[$j]=$i;
	}
    }

    return @str_array;
}


## @returns array for structure
## entry per sequence position: gives paired position or -1
sub parse_bracket_structure {
    my ($str)=@_;
    my @str_array;

    for (my $i=0; $i<length($str); $i++) {
	$str_array[$i]=-1;
    }

    @str_array=parse_bracket_structure_single $str,"(",")",\@str_array;
    @str_array=parse_bracket_structure_single $str,"{","}",\@str_array;
    @str_array=parse_bracket_structure_single $str,"[","]",\@str_array;
    @str_array=parse_bracket_structure_single $str,"<",">",\@str_array;
    @str_array=parse_bracket_structure_single $str,"A","a",\@str_array;
    @str_array=parse_bracket_structure_single $str,"B","b",\@str_array;
    @str_array=parse_bracket_structure_single $str,"C","c",\@str_array;
    @str_array=parse_bracket_structure_single $str,"D","d",\@str_array;
    
    return @str_array;
}


## convert_fix_structure_to_pp($ppfilename,$name,$seq,$str,$constraints)
##
## @param $ppfilename output file name
## @param $name sequence name
## @param $seq sequence string
## @param $str structure string
## @param $constraints anchor constraint string
##
##
sub convert_fix_structure_to_pp($$$$$) {
    my ($ppfilename,$name,$seq,$str,$constraints) = @_;
    
    my @str=parse_bracket_structure($str);
    
    local *OUT;

    open(OUT,">$ppfilename") || die "Cannot write $!";

    print OUT "#PP 2.0\n\n";
    print OUT "$name\t$seq\n";
    
    if (defined $constraints && $constraints ne "") {
    	my @cs = split /\#/, $constraints;
	for (my $i=0; $i<@cs;$i++) {
	    print OUT "#A".($i+1)." ".$cs[$i]."\n";
	}
    }
    print OUT "\n#END\n\n";
    print OUT "#SECTION BASEPAIRS\n\n";
    
    for (my $i=0; $i<=$#str; $i++) {
	if ($str[$i]>$i) {
	    print OUT ($i+1)." ".($str[$i]+1)." 1.0\n";
	}
    }
    print OUT "\n#END\n"; 
}
### ------------------------------------------------------------
### ------------------------------------------------------------


## compute and print reliabilities
##
## @param %aln the multiple alignment
##
sub compute_and_write_reliabilities {
    my ($aln_ref) = @_;
    my %aln = %{$aln_ref};

    my @names = keys %aln;
    @names = grep {!/\#/} @names;

    
    my ($bmrels_seq_ref, $bmrels_str_ref, $amrels_ref);
    
    if ($consistency_transformation) {
	($bmrels_seq_ref, $bmrels_str_ref, $amrels_ref)
	    = compute_reliability(\%aln,\%bmprobs_nocbt,\%amprobs_nocbt);
    } else {
	($bmrels_seq_ref, $bmrels_str_ref, $amrels_ref)
	    = compute_reliability(\%aln,\%bmprobs,\%amprobs);
    }
    
    printmsg 3, "reliability\n";
    
    # compute and print a maximum reliability structure for the consensus reliabilities
    my @empty=();
    my ($score,$rel_str)=max_weight_structure(aln_length(\%aln),\@empty,$amrels_ref,1,0.125);
    printmsg 3, "max. reliability   $rel_str\n";
    
    if (3 >= $MLocarna::Aux::verbosemode) {
	write_reliability_bars($bmrels_seq_ref, $bmrels_str_ref);
    }
    
    write_bm_reliabilities("$tgtdir/$results_dir/result.bmreliability",$bmrels_seq_ref,$bmrels_str_ref);
    write_am_reliabilities("$tgtdir/$results_dir/result.amreliability",$amrels_ref);

    write_dotplot("$tgtdir/$results_dir/reldot.ps",consensus_sequence(\%aln),$amrels_ref);
    
    if ($consistency_transformation) {
	
	my ($bmrels_seq_ref, $bmrels_str_ref, $amrels_ref) 
	    = compute_reliability(\%aln,\%bmprobs,\%amprobs);
	
	if ($verbose) {
	    printmsg 3, "reliability (cbt)\n";
	    if (3 >= $MLocarna::Aux::verbosemode) {
		write_reliability_bars($bmrels_seq_ref, $bmrels_str_ref);
	    }
	}
	
	write_bm_reliabilities("$tgtdir/$results_dir/result.bmreliability-cbt",$bmrels_seq_ref,$bmrels_str_ref);
	write_am_reliabilities("$tgtdir/$results_dir/result.amreliability-cbt",$amrels_ref);

	write_dotplot("$tgtdir/$results_dir/reldot-cbt.ps",consensus_sequence(\%aln),$amrels_ref);
    }
    
    my %reliable_structures; ## hash for storing highly reliable structures (that will be used as constraints)
    
    if ($reliabilities_single_sequences) {
	mkdir "$tgtdir/$results_dir/$single_reliabilities_dir";
	## compute reliability profiles for single sequences
	for my $name (@names) {
	    # printmsg 1,"Compute structure reliability profile for $name\n";
	    
	    ## compute and write reliability profiles for the single sequences
	    my ($bmrels_seq,$bmrels_str) = 
		compute_bmreliabilities_single_seq($name,\%aln,\%bmprobs_nocbt,\%amprobs_nocbt);
	    
	    my $nname = get_normalized_seqname($name);
	    write_bm_reliabilities("$tgtdir/$results_dir/$single_reliabilities_dir/$nname.bmreliability",
				   $bmrels_seq, $bmrels_str);
	    
	    
            ## compute and write arc match reliabilities for the single sequences
	    my %name_pairprobs = read_pp_file_pairprobs("$tgtdir/$input_dir/$nname");
	    
	    my $amrels_ref = compute_amreliabilities_single_seq($name,\%aln,\%amprobs_nocbt,\%name_pairprobs);
	    write_am_reliabilities("$tgtdir/$results_dir/$single_reliabilities_dir/$nname.amreliability",$amrels_ref);
	    write_dotplot("$tgtdir/$results_dir/$single_reliabilities_dir/$nname"."_reldot.ps",$aln{$name},$amrels_ref);
	    
	    my @empty=();
	    my ($score,$rel_str)=max_weight_structure(length($aln{$name}),\@empty,$amrels_ref,1,0.125);
	    
	    printmsg 3,sprintf("%-18s ",$name).$rel_str."\n";
	    
	    $rel_str = project_str_to_aln_seq($rel_str,$aln{$name});
	    
	    $reliable_structures{$name} = $rel_str;
	}
    }

    return \%reliable_structures;
}




## perform the complete multiple alignment for the given sequences
##
## performs the single steps:
## * generate target dir
## * compute of base pair probabilities
## * in probabilistic mode: compute alignment edge probabilities
## * optionally: consistency-transformation
## * compute all-2-all alignments
## * compute guide tree
## * perform progressive steps
## * optionally perform iterative refinement
##
##
## @param $seqs reference to fasta list of hashs
##
## GLOBAL VARS: amprobs, bmprobs, amprobs_nocbt, bmprobs_nocbt (since we need this later)
##
sub perform_multiple_alignment($) {
    my $seqs = shift;
    
    ## ----------------------------------------
    ## get names of sequences in seqs
    #
    my @names = map { $_->{name} } @$seqs;
    
    my $current_dir = abs_path(".");
    
    chdir $tgtdir;
    
    if ($thread_number>1) {
       printmsg 2,"Compute using $thread_number threads.\n";
    }

    ## ----------------------------------------
    # compute probability dot-plots
    # (if wanted or if necessary)
    #
    if ($opt_no_bpp_precomputation) {
	printmsg 3,"Skip precomputation of dot plots.\n";
	generate_input_files_without_bpps($seqs);
    } else {
	printmsg 3,"Compute pair probs ...\n";
	if ($thread_number==1) {
	    compute_all_dotplots($seqs);
	} else  {
	    compute_all_dotplots_par($thread_number,$seqs);
	}
    }
    
    ## exit here if only dps shall be computed
    if ($only_dps) {
	printmsg 1,"Computed only the missing dotplots.\n";
	exit 0;
    }


    ## ----------------------------------------
    ## generate base match probabilities for all sequence pairs
    #  (if required)
    
    if ($probabilistic) {
	#
	# for some strange reasons, later we get problems when using
	# shared variables, where we actually don't need it.
	# Therefore, use extra vars and copy back immediately after
	# use.
	#
	my %bmprobs_shared; # shared for parallel computation
	my %amprobs_shared; # shared for parallel computation
	
	share(\%bmprobs_shared);
	share(\%amprobs_shared);
	
	
	printmsg 3,"Compute match probabilities ...\n";
	
	# compute the base match probabilites
	# either use full partition function locarna_p
	# or use the Gotoh PF implementation in locarna (in fast-mea mode)
	# compute arc match probs only if required 
	#
	#
	if ($thread_number==1) {
	    compute_all_match_probs(\@names, \%bmprobs_shared, \%amprobs_shared);
	} else  {
	    compute_all_match_probs_par($thread_number,\@names, \%bmprobs_shared, \%amprobs_shared);
	}
	
	
	# make unshared clone-copys of shared bmprobs/amprobs
	#
	%bmprobs_nocbt = %{ clone_hash(3, \%bmprobs_shared) };
	%amprobs_nocbt = %{ clone_hash(5, \%amprobs_shared) };
	
	undef %bmprobs_shared; #not needed anymore
	undef %amprobs_shared; #not needed anymore
	
	
	if ($opt_write_bm_probs) {
	    write_bm_probs("$probs_dir/bmprobs",\%bmprobs_nocbt);
	}
	if ($opt_write_am_probs) {
	    write_am_probs("$probs_dir/amprobs",\%amprobs_nocbt);
	}
	
	# print_k_dim_hash(\%bmprobs,4,"");

	if ($consistency_transformation) {
	    ## store untransformed probabilities
	    
	    printmsg 3, "Consistency transform match probabilities ...\n";

	    %bmprobs = consistency_transform_bm(\%bmprobs_nocbt,\@names);	    
	    %amprobs = consistency_transform_am(\%amprobs_nocbt,\@names);
	    
	    if ($opt_write_bm_probs) {
		write_bm_probs("$probs_dir/bmprobs-cbt",\%bmprobs);
	    }
	    if ($opt_write_am_probs) {
		write_am_probs("$probs_dir/amprobs-cbt",\%amprobs);
	    }
	} else {
	    %bmprobs=%bmprobs_nocbt;
	    %amprobs=%amprobs_nocbt;
	}

	#print_k_dim_hash(\%bmprobs,4,"");
    }



    ## ----------------------------------------
    # generate guide tree (if not given)
    #

    my $tree;

    if (defined($tree_file)) {

	printmsg 3,"Read guide tree from file. \n";

	open(TREE,"$tree_file") || die "Cannot read $tree_file for reading tree\n";
	$tree=<TREE>;
	chomp($tree);

	## don't allow names that end in :<float>, but accept input tree with distances (and ignore them)
	$tree =~ s/:[\d.e-]+\s*([,)])/$1/g;
	$tree =~ s/:[\d.e-]+\s*$//g;
	
	close TREE;
	
    } elsif (defined($similarity_matrix_file)) {
	
	my $score_matrix = read_2D_matrix($similarity_matrix_file,int(@names),int(@names));
	
	write_2D_matrix("$results_dir/result.matrix",$score_matrix);
	
	$tree = upgma_tree(\@names,$score_matrix);
		
    } elsif ($#names == 1) { # for only two sequences, tree is unique
	$tree="(".quotemeta($names[0]).",".quotemeta($names[1]).");";
    } elsif ($graphkernel) {
	
	sub noop_system {
	    print "$@\n";
	}
	
	## RECALL: here the current directory is the target directory

	my $cmd="$fasta2shrep $fasta2shrep_options -fasta input/input.fa -tmp graphkernel/tmp -o graphkernel";
	print "Run $cmd\n";

	if (-d "graphkernel") {
	    print "  Remove directory graphkernel\n";
	    File::Path::rmtree "graphkernel";
	}
	
	mkdir "graphkernel";
	mkdir "graphkernel/tmp";
	
	## Run fasta2shrep; writes output directory
	system("$cmd")==0 || die "Cannot run $cmd";
	
	File::Path::rmtree "graphkernel/tmp";
	
	## write results to .gspan file
	system "bzcat graphkernel/*.bz2 > graphkernel/gspan";
	system "rm graphkernel/*.bz2";
	
# note: the gspan format defines graphs

	$cmd = "$svmsgdnspdk -i graphkernel/gspan -r $svmsgdnspdk_radius -d $svmsgdnspdk_distance -gt DIRECTED -a MATRIX";
	print "Run $cmd\n";

	system("$cmd")==0 || die "Cannot run $cmd";
	
	# system "bzip2 graphkernel/gspan"; ## keep the gspan file but compress it
	# OR
	unlink "graphkernel/gspan";
	
	## name of the generated matrix file
	my $matrix_filename="graphkernel/gspan.mtx";
	
	## read matrix file
	my $score_matrix = read_2D_matrix($matrix_filename,int(@names),int(@names));
	
	## convert it
	for my $i (0..@$score_matrix-1) {
	    my $row=$score_matrix->[$i];
	    for my $j (0..@$row-1) {
		$score_matrix->[$i][$j] *= 10000;
	    }
	}
	
	## write it to results directory
	write_2D_matrix("$results_dir/result.matrix",$score_matrix);
	
	$tree = upgma_tree(\@names,$score_matrix);
		
    } else {
	# --------------------------------------------------
	# compute score matrix for guide tree
	# compute pairwise scores in @score_matrix
	#
	
	printmsg 3,"Compute pairwise alignments ... \n";
	
	# store all pairwise alignments in @pairwise_alignments
	my @pairwise_alns;
	if ($thread_number==1) {
	    @pairwise_alns = compute_all_pairwise_alignments(\@names,\%bmprobs,\%amprobs);
	} else  {
	    @pairwise_alns = compute_all_pairwise_alignments_par($thread_number,\@names,\%bmprobs,\%amprobs);
	}
	## fill score matrix
	my $score_matrix = extract_score_matrix_from_alignments(\@names,\@pairwise_alns);
	
	write_2D_matrix("$results_dir/result.matrix",$score_matrix);
	
	if ($ext_library) {
	    extend_library(\@pairwise_alns);
	}
	
	$tree = upgma_tree(\@names,$score_matrix);
	
    } # end generation of tree

    
    open(TREE,">$results_dir/result.tree")|| die "Cannot write to $results_dir/result.tree";
    print TREE "$tree\n";
    close TREE;

    my $tree_postorder = newick_tree_to_postorder($tree);

    if (defined($tree_file)) {
	## check whether tree contains all names in the input file and project tree to these names
	
	if (! check_tree_labels($tree_postorder,\@names) ) {
	    print STDERR "ERROR: given guide tree (--treefile) does not contain all names in the input.\n";
	    exit(-1);
	}
	#print "Tree: @$tree_postorder\n";

	$tree_postorder = project_tree($tree_postorder,\@names);
	
	#print "Projected tree: @$tree_postorder\n";
    }
    
    ## ----------------------------------------
    # progressive alignment along the guide tree
    #
    printmsg 3, "Perform progressive alignment ...\n";

    my $current_alignment_name =
	perform_progressive_steps($tree_postorder,\%bmprobs,\%amprobs);

    ## ----------------------------------------
    ## copy results to files result_prog.aln and result_prog.pp
    ##
    system "cp", "$current_alignment_name.aln", "$results_dir/result_prog.aln";
    system "cp", "$current_alignment_name.pp", "$results_dir/result_prog.pp";


    ### ============================================================
    ### iterative refinement

    ##
    ## 1.) from the guide tree get all bi-partitionings of seq-names,
    ##     that are separated by tree edges 
    ## 2.) start with the final multiple alignment from the progressive phase
    ##     as the current multiple alignment
    ## 3.) for each partitioning
    ##     a) extract the multiple alignments of both partitions out of the current ma
    ##     b) generate new pp files with newly computed consensus pairprobs
    ##     c) align both paritions in order to form a new, better ma
    ##
    ##
    ## Details:
    ## ad 3a) read the current ma and generate the two sub-alignments
    ##        by projection, delete only-gap columns
    ## ad 3b) read all pp files of the single sequences in the sub-alignment
    ##        and compute consensus dot-plot by averaging probs
    ##        (geometric or arithmetic mean).
    ##        This requires two compute the projection of the single sequences
    ##        to their lines in the alignment.
    ##
    ## Handling of constraints?
    ## also: merge constraint lines from the single pp files
    ##
    
    
    if (defined($iterate) && ! defined($iterations)) {
	$iterations=1;
    }

    if (defined($iterations) && ($#names==1)) {
	$iterations = 0;
    }

    if (defined($iterations) && ($iterations>0)) {
 	printmsg 3, "Perform iterative refinement ...\n";

	$current_alignment_name = perform_iterative_refinement($current_alignment_name,$tree_postorder,\@names,\%bmprobs,\%amprobs);
    }
    
    ### DONE iterative refinement
    ### ============================================================
    
    
    ## ----------------------------------------
    # copy results to files result.aln and result.pp
    # (even for non-iterative alignment the result-files are written only here)
    #
    system "cp", "$current_alignment_name.aln", "$results_dir/result.aln";
    system "cp", "$current_alignment_name.pp", "$results_dir/result.pp";
    
    ########################################
    ## rewrite the result alignment result.aln: sort
    ##
    my $aln = read_clustalw_alnloh("$results_dir/result.aln");
    
    if ($keep_sequence_order) {
	$aln = loh_sort($aln,loh_names $seqs);
    }
    
    open(ALNOUT,">$results_dir/result.aln") || die "Cannot write $results_dir/result.aln";
    write_clustalw_alnloh(*ALNOUT,$aln,$width);
    close ALNOUT;
    

    #if (!$verbose || (defined($iterations) && ($iterations>0))) {
	printmsg 3, readpipe("cat $results_dir/result.aln | grep -v '^CLUSTAL W --- LocARNA'");
    #}


    ## ----------------------------------------
    ## compute and show alifold prediction
    
    my $alistr = compute_alifold_structure("$results_dir/result.aln");
    chomp $alistr;

    printmsg 3, "alifold            "."$alistr\n";
    
    # in probabilistic mode compute and store alifold dotplot
    if ($probabilistic) {
	system "$MLocarna::RNAalifold -r -p $results_dir/result.aln >/dev/null 2>&1";
	rename "alidot.ps", "$results_dir/alidot.ps";
    }
    
    rename "alirna.ps", "$results_dir/alirna.ps";
    
    
    chdir $current_dir;
}


### ============================================================================
### ============================================================================
### ============================================================================
### SUBS
###


## compute base and arc match probs for all pairs of names
## we assume that for each name there is a pp file
##
## in fast-mea mode:
## for computing the probabilities, locarna is called with
## option --write-match-probs
##
## in pf mode 
## for computing the probabilities, locarna is called with
## option --write-basematch-probs and --write-arcmatch-probs
##
## returns a hash of "sparse matrices"
sub compute_all_match_probs {
    my ($names_ref,$bmprobs_ref,$amprobs_ref) = @_;

    my @names = @{ $names_ref };
    
    my $a=0;

    for my $nameA (@names) {
	$a++;
	my $b=0;
	for my $nameB (@names) {
	    $b++;
	    if ($b!=$a) {
		compute_match_probs($a,$b,$nameA,$nameB,$bmprobs_ref,$amprobs_ref,$#names+1);
	    }
	}
    }
}

sub compute_all_match_probs_par {
    my ($cpu_num,$names_ref,$bmprobs_ref,$amprobs_ref) = @_;

    my @names = @{ $names_ref };
    
    my @arg_lists_par;
    my @arg_lists_seq;
    
    my $a=0;
    
    ## collect argument lists
    for my $nameA (@names) {
	$a++;
	my $b=0;
	for my $nameB (@names) {
	    $b++;
	    if ($b!=$a) {
		if ($a<$b) {
		    push @arg_lists_par, [ ($a,$b,$nameA,$nameB) ];
		} else {
		    push @arg_lists_seq, [ ($a,$b,$nameA,$nameB) ];
		}
	    }
	}
    }
    
    share($bmprobs_ref);
    share($amprobs_ref);
    
    ## perfom in parallel
    foreach_par(
	sub {
	    my ($a,$b,$nameA,$nameB)=@_;
	    compute_match_probs($a,$b,$nameA,$nameB,$bmprobs_ref,$amprobs_ref,$#names+1);
	},
	\@arg_lists_par,
	$cpu_num);

    
    ## symmetrical entries in sequence
    foreach my $list (@arg_lists_seq) {
	my ($a,$b,$nameA,$nameB)=@{ $list };
	compute_match_probs($a,$b,$nameA,$nameB,$bmprobs_ref,$amprobs_ref,$#names+1);
    }

}

sub compute_match_probs($$$$$$$) {
    my ($a,$b,$nameA,$nameB,$bmprobs_ref,$amprobs_ref,$num_seqs) = @_;
        
    if (exists $bmprobs_ref->{nnamepair($nameB,$nameA)}) {
	# match probs are symmetric. So if we already have it then use it.
	
	my %subhash_bm = transpose_sparsematrix_2D( $bmprobs_ref->{nnamepair($nameB,$nameA)} );
	$bmprobs_ref->{nnamepair($nameA,$nameB)} = clone_share_hash(2, \%subhash_bm);
	
	my %subhash_am = transpose_sparsematrix_4D( $amprobs_ref->{nnamepair($nameB,$nameA)} );
	$amprobs_ref->{nnamepair($nameA,$nameB)} = clone_share_hash(4, \%subhash_am);
	
    } else {
	# compute the match probabilities for the pair
		
	printmsg 2, sprintf(
	    "PFAlign %d/%d : $nameA and %d/%d: $nameB\n",
	    $a,$num_seqs-1,$b,$num_seqs);
	
	my $tmpprefix = threadsafe_name("$global_tmpprefix");
	
	my $tmpfile_bm="$tmpprefix.bmps";
	my $tmpfile_am="$tmpprefix.amps";
	
	my $ref_aln_option = pairwise_reference_alignment_option($nameA,$nameB,$refaln,$refaln_filename);
	
	my $nnameA=get_normalized_seqname($nameA);
	my $nnameB=get_normalized_seqname($nameB);

	my $cmd;
	if ($fast_mea) {
	    $cmd = "$pw_aligner $input_dir/$nnameA $input_dir/$nnameB $locarna_params --write-match-probs $tmpfile_bm";
	} else {
	    $cmd = "$pw_aligner_p $input_dir/$nnameA $input_dir/$nnameB $locarna_p_params --write-basematch-probs $tmpfile_bm";
	    
	    if ( ! $only_basematch_probs ) {
		$cmd .= " --write-arcmatch-probs $tmpfile_am";
	    }
	}
	
	$cmd .= " $ref_aln_option"; ## add option for reference alignment (potentially empty string)
	
	$cmd .= " >/dev/null" unless $moreverbose;
	printmsg 1, "$cmd\n";
	system($cmd)==0 || die "Cannot compute match probabilities by $cmd";
	
	if ((! -e $tmpfile_bm) || (! -e $tmpfile_am)) {
	    die "Pairwise aligner failed to write match probabilities to file; called by $cmd";
	}
	
	#if ($moreverbose) {
	#    system("cat $tmpfile_bm");
	#    system("cat $tmpfile_am");
	#}
	
	my %mat_bm = read_sparsematrix_2D($tmpfile_bm);
	my %mat_am = read_sparsematrix_4D($tmpfile_am);
	

	$bmprobs_ref->{nnamepair($nameA,$nameB)} = clone_share_hash(2,\%mat_bm);
	$amprobs_ref->{nnamepair($nameA,$nameB)} = clone_share_hash(4,\%mat_am);
	
	unlink "$tmpfile_am";
	unlink "$tmpfile_bm";
    }
}



########################################
## clone_share_hash($levels,$x)
##
## make a shared copy of a hash
##
## (one could use ref as "typeof" to make this more generic,
## however its not needed for the moment)
sub clone_share_hash {
    my ($levels,$x) = @_;
    my %y;

    share(\%y);
    
    
    if ($levels>1) {
	foreach my $k (keys %{ $x }) {
	    $y{$k} = clone_share_hash( $levels-1, $x->{$k} );
	}
    } else {
	%y = %{ $x };
    }
    return \%y;
}



## calls pairwise aligner ($pw_aligner) on nameA and nameB in
## input_dir and writes to $tgt.pp and $tgt.aln
sub call_locarna( $$$ ) {
    my ($inA,$inB,$tgt) = @_;
    
    printmsg 2, "Align $inA + $inB --> $tgt\n";
    
    my $cmd="$pw_aligner $inA $inB $locarna_params --clustal=$tgt.aln";
    
    $cmd .= " --pp=$tgt.pp";# unless $alifold_consensus_dp;
    
    $cmd .= " ".reference_alignment_option($refaln_filename);
   
    if (! $moreverbose) { $cmd.=">/dev/null"; }
    
    printmsg 1, "$cmd\n";
    printmsg 1, "\n";
    system($cmd)==0
	|| die "Cannot execute $pw_aligner\n";
    
    if ((! -e "$tgt.aln") || (! -e "$tgt.pp")) {
	die "Pairwise aligner ($pw_aligner) failed to write alignment to file";
    }
    
    system "cat $tgt.aln | grep -v '^CLUSTAL --- LocARNA'" if $verbose;

    
    ## alifold consensus dp is now handled by locarna binary!!!
    # ## optionally, recompute the consensus dot plot using alifold
    # if ($alifold_consensus_dp) {

    # 	## determine version of alifold
    # 	my $alifold_version=readpipe("$MLocarna::RNAalifold --version");
	
    # 	if (grep /RNAalifold 2/, $alifold_version) {
    # 	    $alifold_version=2;
    # 	} else {
    # 	    $alifold_version=1;
    # 	}
	
    # 	$cmd="$MLocarna::RNAalifold -r -p";
    # 	if ($noLP) {
    # 	    if ($alifold_version==2) {
    # 		$cmd .=" --noLP";
    # 	    } else {
    # 		$cmd .=" -noLP";
    # 	    }
    # 	}
    # 	$cmd .= " $tgt.aln";
	
    # 	if (! $moreverbose) { $cmd.=" >/dev/null 2>&1"; }
	
    # 	printmsg 1, "CALL: $cmd\n";
    # 	printmsg 1, "\n";
    # 	system($cmd)==0
    # 	    || die "Cannot execute RNAalifold\n";
	
    # 	convert_alifold_dp_to_pp("alidot.ps","$tgt.aln","$tgt.pp");
    # }
}


## call locarna for aligning alignments in pp files
## pass averaged base and (if required) arc match probs to locarna
## (unless in fast-mea mode, the pair probabilities in the pp files are ignored)
##
sub call_locarna_prob( $$$$$ ) {
    my ($inA,$inB,$tgt,$bmprobs_ref,$amprobs_ref) = @_;
    
    ## extract the alignments from the pp files
    #
    my %alnA = read_pp_file_aln_wo_anno($inA);
    my %alnB = read_pp_file_aln_wo_anno($inB);
    
    my $tmpprefix = threadsafe_name("$global_tmpprefix");
    
    # average the base match probabilities and write to tmpfile
    my %m = average_basematch_probs( \%alnA, \%alnB, $bmprobs_ref );
    my $tmpfile_bm="$tmpprefix.bmps";
    write_sparsematrix_2D(\%m,$tmpfile_bm);
    my $readprob_args = "--read-match-probs=$tmpfile_bm";
    
    my $tmpfile_am="$tmpprefix.amps";
    # if required, average the arc match probabilities and write to tmpfile
    if (!$fast_mea && !$only_basematch_probs) {
	my %m = average_arcmatch_probs( \%alnA, \%alnB, $amprobs_ref );
	write_sparsematrix_4D(\%m,$tmpfile_am);
	
	$readprob_args .= " --read-arcmatch-probs=$tmpfile_am";
    }
    
    printmsg 2, "Align $inA + $inB --> $tgt\n";
    my $cmd="$pw_aligner $inA $inB $locarna_params --pp=$tgt.pp --clustal=$tgt.aln $readprob_args";

    $cmd.=" ".reference_alignment_option($refaln_filename);

    if (! $moreverbose) { $cmd.=" >/dev/null"; }
    
    printmsg 1, "$cmd\n\n";
    
    system($cmd)==0
	|| die "Cannot execute $pw_aligner\n";
    
    if ((! -e "$tgt.aln") || (! -e "$tgt.pp")) {
	die "Pairwise aligner ($pw_aligner) failed to write alignment to file";
    }

    system "cat $tgt.aln" if ($verbose);
    
    if (!$fast_mea && !$only_basematch_probs) {
	unlink $tmpfile_am;
    }
    unlink $tmpfile_bm;
}

########################################
# write pp file out of the alignment projected to
# the given names
# generates consensus dotplot and constraints
# out of single pp files
sub write_pp_projected($$$) {
    my ($aln_ref, $names_ref, $tgt_name) = @_;
    my %aln = %{ $aln_ref };
    my @names = @{ $names_ref };
    
    my %alnP = project_aln(\%aln,\@names);

    ## compute the consensus dot plot
    my @consensus_dp; # 2D array
    my @size; # number of added values for pos $i,$j
    
    foreach my $name (@names) {
	my @posmap = project_seq($alnP{$name});
	my %name_aln =
	    read_pp_file_aln_w_anno("$input_dir/".get_normalized_seqname($name));	
	my %name_pairprobs =
	    read_pp_file_pairprobs("$input_dir/".get_normalized_seqname($name));	
	
	if ( exists $name_aln{"#C"} && (!$ignore_constraints)) {
	    # my $constraints = $name_aln{"#C"};
	    printerr "WARNING: constraints ignored in iterative alignment.\n";
	}
	
	# for computing the geometric mean, we don't allow 0 values,
	# since this extinguishs the probability completely!
	# therfore we use some very small 'background' probability
	# if $p is too low.
	#
	# In principle, copy the magic spell from alignment.cc
	#
	my $minimal_p = $min_prob*0.75;
	if (defined($exp_prob) && $exp_prob<$minimal_p ) {
	    $minimal_p = $exp_prob;
	}
		
	for my $pair (keys %name_pairprobs) {
	    $pair =~ /(\d+) (\d+)/;
	    my $i=$1;
	    my $j=$2;
	    my $p = $name_pairprobs{$pair};

	    if ($p<$minimal_p) {$p=$minimal_p;}
	    
	    $consensus_dp[$posmap[$i]][$posmap[$j]] += log($p);
	    $size[$posmap[$i]][$posmap[$j]] ++;
	}
    }
    

    # normalize consensus_dp
    my $len  = aln_length(\%alnP);
    
    for (my $i=1; $i<=$len; $i++) {
	for (my $j=$i+1; $j<=$len; $j++) {
	    if (defined($consensus_dp[$i][$j])) {
		$consensus_dp[$i][$j] = exp($consensus_dp[$i][$j] / $size[$i][$j]);
	    }
	}
    }
    
    write_pp($tgt_name,\%alnP,\@consensus_dp,$min_prob);
    
    return;
}



sub extend_library($$) {
    my ($pairwise_alns_ref,$names_ref) = @_;
    my @pairwise_alns = @{ $pairwise_alns_ref };
    
    my @names=@{ $names_ref };

    ### ------------------------------------------------------------
    ## compute "extended pairprobability library"
    #
    
    my @pairprobs;
    my @sequences;
    
    for (my $a=0; $a<=$#names; $a++) {
	my $seqA;
	my $pairprobsA_ref;
	($seqA,$pairprobsA_ref) = read_dp_ps("$names[$a]");
	
	$sequences[$a] = $seqA;
	$pairprobs[$a] = [ @{ $pairprobsA_ref } ];
    }
    
    printmsg 3, "Compute extended pairprobability library ...\n";
    
    ## make a deep copy of pair probabilities
    #
    my @ext_pairprobs;
    for (my $a=0; $a<=$#names; $a++) {
	my @eppA;
	my @ppA = @{ $pairprobs[$a] };
	for my $i ( 0..$#ppA ) {
	    if (defined $ppA[$i]) {
		my @ppAa = @{ $ppA[$i] };
		for my $j ( 0..$#ppAa ) {
		    if (defined $ppAa[$j]) {
			$eppA[$i][$j] = $ppA[$i][$j];
		    }
		}
	    }
	}
	$ext_pairprobs[$a] = [ @eppA ];
    }
    
    for (my $a=0; $a<=$#names; $a++) {
	printmsg 1, "Compute ext lib for $names[$a]\n";
	
	my @ext_pairprobsA = @{ $ext_pairprobs[$a] }; # we accummulate the extension!
	
	for (my $b=0; $b<=$#names; $b++) {
	    if ($a != $b) {
		
		my $aliA; # alignment-string for sequence a
		my $aliB; # alignment-string for sequence b
		
		if ($a>$b) {
		    my @alnAB = @{ $pairwise_alns[$a][$b] };
		    ($aliA,$aliB) = extract_from_clustal_alignment($names[$a],$names[$b],\@alnAB);
		} else {
		    my @alnBA = @{ $pairwise_alns[$b][$a] };
		    ($aliB,$aliA) = extract_from_clustal_alignment($names[$b],$names[$a],\@alnBA);
		}
		
		my @sp2apA = seqpos_to_alipos($aliA);
		my @sp2apB = seqpos_to_alipos($aliB);
		
		my @ap2spA = alipos_to_seqpos($aliA);
		my @ap2spB = alipos_to_seqpos($aliB);
		
		my $lengthA=$#sp2apA+1;
		my $lengthB=$#sp2apB+1;
		
		printmsg 1, "  $a:$names[$a] ($lengthA) --- $b:$names[$b]\n";
		
		my @pairprobsB = @{ $pairprobs[$b] };
		
		for (my $iA=0; $iA<$lengthA; $iA++) {
		    for (my $jA=$iA+1; $jA<$lengthA; $jA++) {
			my $pA = (defined $ext_pairprobsA[$iA][$jA])?$ext_pairprobsA[$iA][$jA]:$min_prob;
			if ($pA > $min_prob) {
			    my $iB = $ap2spB[$sp2apA[$iA]];
			    my $jB = $ap2spB[$sp2apA[$jA]];
			    
			    if ($iB != -1 && $jB != -1) {
				# print "$iA,$jA <--> $sp2apA[$iA],$sp2apA[$jA] <--> $iB,$jB\n";
				my $pB = (defined $pairprobsB[$iB][$jB])?$pairprobsB[$iB][$jB]:$min_prob;
				if ($pB>$min_prob) {
				    my $new_pA = exp( log($pA) * (1/(1+$pB)));
				    $ext_pairprobsA[$iA][$jA] = $new_pA;
				    my @orig_pairprobsA=@{ $pairprobs[$a] };
				    my $orig_pA  = $orig_pairprobsA[$iA][$jA];
				    printmsg 1, "Enhance ($iA,$jA):$pA by ($iA,$jB):$pB --> $new_pA/$orig_pA\n";
				}
			    }
			}
		    }
		}
	    }
	}
    }
    
    ## --------------------------------------------------
    ## write pp files with extended probabilities
    #
    
    for (my $a=0; $a<=$#names; $a++) {
	# print "write extended probs for $names[$a]\n";

	my $seq=$sequences[$a];
	my $len=length($seq);
	my @ext_pairprobs = @{ $ext_pairprobs[$a] }; # the extended probs
	

	open(PP_OUT, ">$names[$a].pp") || die "Cannot write to $names[$a].pp\n"; 
	
	print PP_OUT "$names[$a] $seq\n\n#\n";
	
	for (my $i=0; $i<$len; $i++) {
	    for (my $j=$i+1; $j<$len; $j++) {
		if (defined $ext_pairprobs[$i][$j]) {
		    my $p = $ext_pairprobs[$i][$j];
		    if ($p > $min_prob) {
			print PP_OUT "$i $j $p\n";
		    }
		}
	    }
	}
    }
    
    #
    # print "\n\n";
    #

    for (my $a=0; $a<=$#names; $a++) {
	rename "$names[$a].pp", "$names[$a]";
    }
    
    #
    ##
    ## --------------------------------------------------
}


## compute the alignment from dp-files or pp-files
sub compute_alignment_from_dps( $$$$ ) {
    my ($input_dir,$nameA,$nameB,$locarna_params) =  @_;
    
    local *CA_IN;

    my $tmpprefix = threadsafe_name("$global_tmpprefix");
    
    my $tmpfile="$tmpprefix.clustal";

    my $seqA_dp="$input_dir/".get_normalized_seqname($nameA);
    my $seqB_dp="$input_dir/".get_normalized_seqname($nameB);

    my $cmd="$pw_aligner $seqA_dp $seqB_dp $locarna_params --clustal=$tmpfile >/dev/null";
    
    $cmd .= " ".pairwise_reference_alignment_option($nameA,$nameB,$refaln,$refaln_filename);
        
    system $cmd || die "Command $cmd failed."; 
    
    open(CA_IN,"$tmpfile") || 
	die 
	"\nPairwise aligner failed to write alignment ($tmpfile.)\n  Failed call:\n    $cmd\n";
	
    
    my @content=<CA_IN>;
   
    close CA_IN;
    
    unlink $tmpfile;
    
    return @content;
}

## compute alignment with given probabilities
sub compute_alignment_from_dps_probs( $$$$$$ ) {
    my ($input_dir,$nameA,$nameB,$locarna_params,$bmprobs_ref,$amprobs_ref) =  @_;
    
    # printerr "compute_alignment_from_dps_probs $seqA_dp $seqB_dp";
    
    local *CA_IN;
    
    my $tmpprefix = threadsafe_name("$global_tmpprefix");

    my $tmpfile  = "$tmpprefix.clustal";
    my $tmpfile2 = "$tmpprefix.bmprobs";
    my $tmpfile3 = "$tmpprefix.amprobs";

    my $seqA_dp="$input_dir/".get_normalized_seqname($nameA);
    my $seqB_dp="$input_dir/".get_normalized_seqname($nameB);

    write_sparsematrix_2D($bmprobs_ref,$tmpfile2);
    
    $locarna_params .= " --clustal=$tmpfile --read-match-probs=$tmpfile2";
    
    if (!$fast_mea && !$only_basematch_probs) {
	write_sparsematrix_4D($amprobs_ref,$tmpfile3);
   
	$locarna_params .= " --read-arcmatch-probs=$tmpfile3";
    }
    
    my $cmd="$pw_aligner $seqA_dp $seqB_dp $locarna_params >/dev/null";
    
    $cmd .= " ".pairwise_reference_alignment_option($nameA,$nameB,$refaln,$refaln_filename);
    
    system $cmd || die "Command $cmd failed."; 
   
    open(CA_IN,"$tmpfile") || die "compute_alignment_from_dps_probs: Cannot read temporary file $tmpfile\n";

    if (!$fast_mea && !$only_basematch_probs) {
	unlink $tmpfile3;
    }
    unlink $tmpfile2;

    
    my @content=<CA_IN>;
    
    close CA_IN;
    
    unlink $tmpfile;
        
    return @content;
}



## ----------------------------------------
## compute the dotplots of all sequences
## of given names in a given sequences hash
## 
## write to files in the current directory
## (filenames equal the sequence names)
##
## ------------------------------
## Dependency on GLOBAL VARIABLES
##
## $dp_cache   if defined, use as cache directory
##             in order to avoid multiple computation of bp probs
## $skip_pp    if true, skip computation of bp-probs for RNAs
##             where the corresponding file already exists in
##             the current directory
## $rnafold_parameter_file
##
## $verbose
## $plfold_span
## $plfold_winsize
##
sub compute_all_dotplots($) {
    my ($seqs) = @_;
    
    for my $i (0..@$seqs-1) {
	compute_dotplot($i,$seqs);
    }
}


sub compute_all_dotplots_par($$$) {
    my ($cpu_num,$seqs) = @_;
    
    my @arg_list;
    foreach my $i (0..@$seqs-1) {
	push @arg_list, [ ($i) ];
    }

    foreach_par(sub { my ($i)=@_; compute_dotplot($i,$seqs) },
	    \@arg_list,
	    $cpu_num);
}

## ----------------------------------------
## generate input files without base pair probabilties
sub generate_input_files_without_bpps( $ ) {
    my ($seqs) = @_;
    for my $i (0..@$seqs-1) {
	my $name = $seqs->[$i]->{name}; # the name of the sequence
	my $ppname="$input_dir/".get_normalized_seqname($name);
	my $seq_str = $seqs->[$i]->{seq}; ## the sequence string
	
	if (! $ignore_constraints) {
	    my $constraints = sequence_constraint_string($seqs->[$i]);
	    if (defined($constraints) && ($constraints ne "")) {
		print "WARNING: Constraints found for sequence $i, but not supported without precomputation of base pair probabilities\n."
	    }
	}
	
	## write a simple fasta file with the input file name
	my $fh;
	open($fh, ">$ppname") || die "Cannot write to $ppname.\n";
	
	print $fh ">$name\n";
	print $fh "$seq_str\n";
	
	close ($fh);
    }
}

############################################################
## compute_dotplot(i,seqs)
## compute probability dotplot for the $ith sequence in $seqs (using RNAfold/RNAplfold)
## 
sub compute_dotplot($$) {
    my ($i,$seqs)=@_;
    
    my $RNAfold_args="";
    
    if (defined($rnafold_parameter_file)) {
	$RNAfold_args.=" -P $rnafold_parameter_file";
    }

    my $tmpname = threadsafe_name("$global_tmpname");

    my $constraints;
    if (! $ignore_constraints) {
	$constraints = sequence_constraint_string($seqs->[$i]);
    }
    
    my $name = $seqs->[$i]->{name}; # the name of the sequence
    
    my $ppname="$input_dir/".get_normalized_seqname($name);
    
    
    ## decide whether we can use locarna_rnafold_pp as a short cut for
    ## generating pp files by setting these variables:
    my $RNAfoldp;
    my $RNAfoldp_redirect="";
    my $convert_to_pp=1;
    my $seqname;
    ## Note: in the case of using locarna_rnafold_pp, we do directly
    ## produce a the pp file in the input sub-directory. Since no dp file is produced,
    ## caching of dp files does not work.
    ##
    if ($HAVE_RNAFOLD_PP && ($RNAfold_args eq "") && (!defined($plfold_span)) 
	&& (!$stacking) && ((! defined($constraints)) || ($constraints eq ""))) {
	$RNAfoldp = "$bindir/locarna_rnafold_pp -p $min_prob ";
	if ($opt_in_loop_probabilities) {
	    $RNAfoldp.="--in-loop ";
	}
	$convert_to_pp=0;
	$RNAfoldp_redirect=" > $ppname";
	$seqname=$name;
    } else {
	if ($opt_in_loop_probabilities) {
	    print STDERR "Warning: cannot precompute in-loop probabilities.\n";
	}
	$RNAfold_args.=" >/dev/null" unless $verbose;
	$seqname=$tmpname;
	if (! defined($plfold_span)) {
	    $RNAfoldp = "$RNAfold -p2 $RNAfold_args";
	} else {
	    $RNAfoldp = "$RNAplfold -L $plfold_span -W $plfold_winsize $RNAfold_args";
	}
    }

    my $seq_str = $seqs->[$i]->{seq}; ## the sequence string
    
    ##
    ## implement optional skipping of input dot plot files:
    ## if skip_pp is given and one of the files $ppname, $ppname\_dp2.ps, or $ppname\_dp.ps
    ## is available, no new dot plot is computed.
    ## Test in the above order, so $ppname overrides $ppname\_dp2.ps overrides or $ppname\_dp.ps
    ##
    ## The latter two cases allow using anchor constraints given in the input mfasta file
    ##
    ##
    if ( $skip_pp && (-e "$ppname") ) {
	# assume that the pp file is in pp format
	print "Skip $name\n" if $moreverbose;
        # skip
    } elsif ( $skip_pp && (-e "$ppname\_dp2.ps") ) {
	# assume that the file is in dp2 format. Therefore, convert to pp.
	print "Skip $name, convert from $ppname\_dp2.ps\n"  if $moreverbose;
        convert_dp_to_pp_with_constraints("$ppname\_dp2.ps",$ppname,$name,$seq_str,$constraints,1);
    } elsif ( $skip_pp && (-e "$ppname\_dp2.ps") ) {
        # assume that the file is in dp format. Therefore, convert to pp.
	print "Skip $name, convert from $ppname\_dp.ps\n"  if $moreverbose;
	convert_dp_to_pp_with_constraints("$ppname\_dp.ps",$ppname,$name,$seq_str,$constraints,0);
    } else {
	## ----------------------------------------
	# partition folding
	#
	
	my $convert_to_pp_this=$convert_to_pp;
	
	my $longname=$seqs->[$i]->{name};
	$longname =~ s/[^a-zA-Z0-9]/_/g; # replace special characters by "_" such that this can be used as filename again
	
	my $dpname;
	
	my $new_dp_computed=0; # whether a new dot plot was computed for $name
	
	if ( !( (defined($dp_cache)) && ( -e "$dp_cache/$longname\_dp2.ps" ) ) ) {
	    $new_dp_computed=1;
	    
	    ## handle cases where structure constraints are specified
	    ## fixed structure #FS overrides structure #S
	    ##
	    if ((!$ignore_constraints) && exists $seqs->[$i]->{"ANNO\#FS"}) {
		my $structure = $seqs->[$i]->{"ANNO\#FS"};
		convert_fix_structure_to_pp($ppname,$name,$seq_str,$structure,$constraints);
		$convert_to_pp_this=0;
	    }
	    elsif ((!$ignore_constraints) && exists $seqs->[$i]->{"ANNO\#S"}) {
		# if a structure constraint is defined do constraint folding
		my $constraint_string = $seqs->[$i]->{"ANNO\#S"};
		
		if ( $RNAfoldp =~ /locarna_rnafold_pp/ ) { ## which input format? for RNAfold or locarna_rnafold_pp?
		    systemverb_withinput("$seqname $seq_str\n\#S $constraint_string\n",
					 "$RNAfoldp -C $RNAfoldp_redirect");
		} else { 
		    systemverb_withinput(">$seqname\n$seq_str\n$constraint_string\n",
					 "$RNAfoldp -C $RNAfoldp_redirect");
		}
	    } else {
		systemverb_withinput(">$seqname\n$seq_str\n",
				     "$RNAfoldp $RNAfoldp_redirect");
	    }
	    
	    if (defined($dp_cache)) {
		system "cp", "$tmpname\_dp.ps","$dpname\_dp.ps";
		if ( -e "$tmpname\_dp2.ps" ) {system "cp", "$tmpname\_dp2.ps","$dpname\_dp2.ps";}
	    }
	    
	    $dpname=$tmpname;
	} else {
	    $dpname="$dp_cache/$longname";
	}

	if ($convert_to_pp_this) {
	    if ( -e "$dpname\_dp2.ps" ) {
		convert_dp_to_pp_with_constraints("$dpname\_dp2.ps",$ppname,$name,$seq_str,$constraints,1);
	    } else {
		convert_dp_to_pp_with_constraints("$dpname\_dp.ps",$ppname,$name,$seq_str,$constraints,0);
	    }
	    
	    if ($new_dp_computed) {
		unlink "$dpname\_ss.ps" || printerr "Cannot delete $tmpname\_ss.ps\n";
		unlink "$dpname\_dp.ps" || printerr "Cannot delete $tmpname\_dp.ps\n";
		if ( -e "$tmpname\_dp2.ps" )  { unlink "$dpname\_dp2.ps" || printerr "Cannot delete $tmpname\_dp2.ps\n"; }
	    }
	}
    }
}


## ----------------------------------------
## compute all pairwise alignments for pairs
## of sequences (given by @names)
##
## @returns 2D-array of alignments (indices are
##          positions in list @names) 
##
## Dependencies on GLOBAL VARIABLES
##
## $probabilistic   flag for probabilistic alignment
## %bmprobs         base match probs in case of probabilistic alignment
## %amprobs         arc  match probs in case of probabilistic alignment 
##
sub compute_all_pairwise_alignments($$$) {
    my ($names_ref,$bmprobs_ref,$amprobs_ref) = @_;

    my %bmprobs = %{ $bmprobs_ref };
    my %amprobs = %{ $amprobs_ref };


    my @names = @{ $names_ref };
    
    my @pairwise_alns;

    my $num=0;
    for (my $a=0; $a<=$#names; $a++) {
	for (my $b=0; $b<$a; $b++) {
	    $num++;
	    
	    printmsg 2, sprintf(
		"Align %d/%d : $names[$a] and %d/%d: $names[$b] (%d/%d)\n",
		$a+1,$#names+1,$b+1,$a,$num,($#names*($#names+1))/2);
	    
	    my @aln;
	    if ( $probabilistic ) {
		@aln =
		    compute_alignment_from_dps_probs($input_dir,
						     $names[$a],
						     $names[$b],
						     $locarna_params_tree,
						     $bmprobs{nnamepair($names[$a],$names[$b])},
						     $amprobs{nnamepair($names[$a],$names[$b])}
		    );
	    } else {
		@aln = compute_alignment_from_dps($input_dir,
						  $names[$a],
						  $names[$b],
						  $locarna_params_tree
		    );
	    }
	    
	    $pairwise_alns[$a][$b] = [ @aln ];
	}
    }
    return @pairwise_alns;
}


sub compute_all_pairwise_alignments_par($$$$) {
    my ($cpu_num,$names_ref,$bmprobs_ref,$amprobs_ref) = @_;

    my %bmprobs = %{ $bmprobs_ref };
    my %amprobs = %{ $amprobs_ref };


    my @names = @{ $names_ref };
    
    my @argument_lists;
    
    my @pairwise_alns : shared;

    ## collect arguments for function calls (=jobs)
    my $num=0;
    for (my $a=0; $a<=$#names; $a++) {
	for (my $b=0; $b<$a; $b++) {
	    $num++;
	    
	    if ( $probabilistic ) {
		my @arg_list = ($num,$a,$b,
				$names[$a],
				$names[$b],
				$bmprobs{nnamepair($names[$a],$names[$b])},
				$amprobs{nnamepair($names[$a],$names[$b])});
		push @argument_lists, [ @arg_list ];
	    } else {
		my @arg_list=($num,$a,$b,
			      $names[$a],
			      $names[$b]
		    );
		push @argument_lists, [ @arg_list ];
	    }
	}
    }

    ## perform jobs in parallel
    foreach_par(
	sub {
	    my ($num,$a,$b,@arg_list)=@_;
	    
	    printmsg 2, sprintf(
		"Align %d/%d : $names[$a] and %d/%d: $names[$b] (%d/%d)\n",
		$a+1,$#names+1,$b+1,$a,$num,($#names*($#names+1))/2);
	    
	    my @aln;
	    
	    if ( $probabilistic ) {
		my ($nameA,$nameB,$bmprobs,$amprobs) = @arg_list;
		@aln = compute_alignment_from_dps_probs($input_dir,
							$nameA,
							$nameB,
							$locarna_params_tree,
							$bmprobs,
							$amprobs);
	    } else {
		my ($nameA,$nameB) = @arg_list;
		@aln = compute_alignment_from_dps($input_dir,
						  $nameA,
						  $nameB,
						  $locarna_params_tree
						  );
	    }
	    
	    $pairwise_alns[$a*($#names+1)+$b] = "@aln";
	    
	},
	\@argument_lists,
	$cpu_num
	);
    

    ## convert result for returning
    my @res;
    
    for (my $a=0; $a<=$#names; $a++) {
	for (my $b=0; $b<$a; $b++) {
	    my @aln =  split(/\n/, $pairwise_alns[$a*($#names+1)+$b]);
	    $res[$a][$b] = [ @aln ];
	}
    }

    return @res;
}


## ----------------------------------------
## perform the progressive steps
## for getting the multiple alignment
##
## args @tree_postorder defines the order of alignments (=guide tree)
##
## Dependencies
##
## needs pp files
## needs bmprobs, amprobs in probabilistic mode
##
sub perform_progressive_steps($$$) {
    my ($tree_postorder,$bmprobs_ref,$amprobs_ref)=@_;    
    
    my @stack=();
        
    for my $item (@$tree_postorder) {
	if ($item eq $MLocarna::Trees::node_sym) {
	    # align two topmost stack entrys and push result
	    my $op1 = $stack[$#stack-1];
	    my $op2 = $stack[$#stack];
	    
	    $#stack -= 2;
	    
	    my $intermediate_name = new_intermediate_name();
	    
	    if ( $probabilistic ) {
		call_locarna_prob($op1,$op2,"$intermediate_dir/".$intermediate_name,$bmprobs_ref,$amprobs_ref);
	    } else {
		call_locarna($op1,$op2,"$intermediate_dir/".$intermediate_name);
	    }
	    
	    push @stack, "$intermediate_dir/$intermediate_name.pp";
	    
	} else {
	    push @stack, "$input_dir/".get_normalized_seqname($item);
	}
    }
    
    if ($#stack!=0) {
	printerr "Tree parsing failed. Exit.\n";
	printerr "STACK: @stack\n";
	exit -1;
    }

    (my $current_alignment_name = $stack[-1] ) =~ s/.pp$//;

    return $current_alignment_name;
}


## ----------------------------------------
## Perform iterative refinement
##
## @returns file name of result
##
sub perform_iterative_refinement {
    my ($current_alignment_name,$tree_postorder_ref,$names_ref,$bmprobs_ref,$amprobs_ref) = @_;
    
    ## my $current_aliquality = alifold_mfe("$current_alignment_name.aln"); ## optimize SCI
    
    my $current_aliquality;
    if ($probabilistic) {
	$current_aliquality = aln_reliability_fromfile("$current_alignment_name.aln",\%bmprobs,\%amprobs); ## optimize RELIABILITY
    } else {
	$current_aliquality = - alifold_mfe("$current_alignment_name.aln"); ## optimize SCI
    }

    for (my $i=1; $i<=$iterations; $i++) {
	printmsg 1, "--- Iterative Refinement ---\n";
	printmsg 1, "--- Round $i\n";
	
	my $round_aliquality;
	($current_alignment_name,$round_aliquality) =
	    perform_iterative_refinement_one_round($current_alignment_name,$current_aliquality,$tree_postorder_ref,$names_ref,$bmprobs_ref,$amprobs_ref);
	
	if ($current_aliquality == $round_aliquality) {
	    printmsg 2, "Converge after round $i\n\n";
	    last;
	}
	$current_aliquality=$round_aliquality;
    }
    return $current_alignment_name;
}

## ----------------------------------------
## Perform one round of iterative refinement
## 
## @returns file name of result and alignment quality
##
sub perform_iterative_refinement_one_round($$$$$) {
    my ($current_alignment_name,$current_aliquality,$tree_postorder,$names_ref,$bmprobs_ref,$amprobs_ref) = @_;
    
    my $round_aliquality = $current_aliquality;
    
    my @names = @{ $names_ref };


    my $partitions = tree_partitions($tree_postorder);
    
    foreach my $namesA_ref (@$partitions) {
	my @namesA = @{ $namesA_ref };
	my @namesB = subtract_list( \@names, \@namesA );
	
	printmsg 2, "Realign split @namesA --- @namesB\n";
	
	my $aln = read_aln_wo_anno("$current_alignment_name.aln");
	
	my $intermediate_name = "$intermediate_dir/".new_intermediate_name();
	
	my $intermediate_nameA = "$intermediate_name.A.pp";
	my $intermediate_nameB = "$intermediate_name.B.pp";
	write_pp_projected($aln,\@namesA,$intermediate_nameA);
	write_pp_projected($aln,\@namesB,$intermediate_nameB);
	
	
	if ( $probabilistic ) {
	    call_locarna_prob($intermediate_nameA,$intermediate_nameB,$intermediate_name,$bmprobs_ref,$amprobs_ref);
	} else {
	    call_locarna($intermediate_nameA,$intermediate_nameB,$intermediate_name);
	}
	
	## ----------------------------------------
	# test, whether new alignment is better than former
	# 
	my $new_aliquality;# = alifold_mfe("$intermediate_name.aln");
	if ($probabilistic) {
	    $new_aliquality = aln_reliability_fromfile("$intermediate_name.aln",\%bmprobs,\%amprobs); ## optimize RELIABILITY
	} else {
	    $new_aliquality = - alifold_mfe("$intermediate_name.aln"); ## optimize SCI
	}
	printmsg 2, sprintf "Quality: %5.2f / %5.2f\n",$new_aliquality,$current_aliquality;

	if ($new_aliquality > $current_aliquality) {
	    printmsg 0, "Accept new alignment (improves alifold-mfe: $current_aliquality ==> $new_aliquality)\n";
	    $current_aliquality = $new_aliquality;
	    $current_alignment_name = $intermediate_name;
	} else {
	    printmsg 0, "Reject new alignment (worse or equal alifold-mfe: $current_aliquality <= $new_aliquality)\n";
	}
	printmsg 0, "\n";
    }
    
    return ($current_alignment_name,$current_aliquality);
}




## EVALUATION MODE
## to keep this simple, assume that probabilities
## are already computed and given in tgtdir
sub run_evaluation_mode {
    my ($alnfile,$probsdir)=@_;
    
    ## TODO: this should be changed for use of alnloh representation
    ## such that sequence name order is preserved
    my $aln;
    
    if ( $opt_eval_fasta ) {
 	my $alnlist = read_fasta($alnfile);
	my %alnhash=();
	foreach my $seq (@$alnlist) {
	    $alnhash{$seq->{name}} = $seq->{seq};
	}
	$aln = \%alnhash;
    } else {
	$aln = read_aln_wo_anno($alnfile);
    }
    
     ## register normalized names that can be used as filenames
    for my $name (keys %$aln) {
	register_normalized_seqname($name);
    }
    
    my $bmprobs = read_bm_probs("$probsdir/bmprobs");
    my $amprobs = read_am_probs("$probsdir/amprobs");
    
    write_aln($aln);
    printmsg 3, "\n";

    
    my $cheap_score = aln_reliability($aln,$bmprobs,$amprobs);
    
    my ($score,$rel_str) = evaluate_alignment($aln,$bmprobs,$amprobs,1);
    
    printmsg 3, "RELIABILITY 1/COL   ".sprintf("%5.2f",$cheap_score)."\%\n";
    printmsg 3, "RELIABILITY 2/COL  ".sprintf("%5.2f",$score)."\%\n";
    printmsg 3, "MAX REL. STRUCT.   $rel_str\n";
    
    my $len = aln_length($aln);
    my $corrected_len=aln_length_atleastonematch($aln);

    my $corrected_score=$score*$len/$corrected_len;
    my $corrected_cheap_score=$cheap_score*$len/$corrected_len;
    #
    printmsg 3, "RELIABILITY 1/CCOL  ".sprintf("%5.2f",$corrected_cheap_score)."\%\n";
    printmsg 3, "RELIABILITY 2/CCOL  ".sprintf("%5.2f",$corrected_score)."\%\n";
    
    
}

########################################
## pairwise_reference_alignment_option($nameA,$nameB,$aln)
##
## generate option for pairwise reference alignment of sequences 
## with names $nameA and $nameB in the multiple alignment $aln
##
## current implementation returns option for passing the whole
## multiple alignment to the locarna executable. Instead, one could
## pass only the pairwise alignment, avoiding some IO.
##
## @pre $aln contains sequences $nameA and $nameB 
##
## @param $nameA name of sequence A
## @param $nameB name of sequence B
## @param $refaln reference alignment
## @param $refaln_filename name of file with reference alignment
##
## @returns option string or "" if $aln undefined
sub pairwise_reference_alignment_option( $$$$ ) {
    my ($nameA,$nameB,$refaln,$refaln_filename) = @_;
    return reference_alignment_option($refaln_filename);
}

########################################
## reference_alignment_option($aln)
##
## return option for passing the alignment $aln as reference alignment
## to locarna
##
## @param $refaln_filename name of file with reference alignment
##
## @returns option string or "" if $refaln_filename undefined
sub reference_alignment_option {
    my ($refaln_filename) =@_;
    if (defined( $refaln_filename )) {
	return "--max-diff-aln=\"$refaln_filename\"";
    } else {
	return "";
    }
}

########################################
## is_aln_of_seqs($seqs,$reference_aln)
##
## check whether $reference_aln contains an alignment of the sequences $seq
## 
## @param $seqs list of sequences with names (as returned by read_fasta)
## @param $reference_aln reference alignment in loh (as returned by read_aln_wo_anno)
##
## @returns boolean whether $reference_aln contains an alignment of the sequences $seq
##
sub is_aln_of_seqs {
    my ($seqs,$aln) = @_;
    
    foreach my $seq ( @$seqs ) {
	my $found=0;
	foreach my $alnseq ( @$aln ) {
	    if ($seq->{name} eq $alnseq->{name}) {
		

		my $alnseqstr = $alnseq->{seq};
		$alnseqstr =~ tr/~.-//d;
		$alnseqstr = uc $alnseqstr;
		$alnseqstr =~ s/T/U/g;

		my $seqstr=$seq->{seq};
		$seqstr = uc $seqstr;
		$seqstr =~ s/T/U/g;

		$found = ( $alnseqstr eq $seqstr );
		
		if ($verbose && !$found) {
		    print STDERR "Warning: name match ($seq->{name}) but sequence clash between input and reference alignment!";
		    print STDERR "  Reference: $alnseqstr\n";
		    print STDERR "  Input    : $alnseqstr\n";
		}

		last;
	    }
	}
	if (!$found) { 
	    return 0;
	}
    }
    return 1;
}

### ============================================================================
### ============================================================================
